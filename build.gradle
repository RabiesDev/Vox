buildscript {
    repositories {
        mavenCentral()
        maven {
            url = 'https://maven.minecraftforge.net'
        }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7.+'
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.github.johnrengelman.shadow'

group = 'dev.rabies'
version = '0.1.1'

java {
    archivesBaseName = 'vox'
    toolchain.languageVersion = JavaLanguageVersion.of(8)
}

minecraft {
    mappings channel: 'stable', version: '39-1.12'
    accessTransformer = file('src/main/resources/vox_at.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            property 'fml.coreMods.load', 'dev.rabies.vox.MixinLoader'
            property 'mixin.env.disableRefMap', 'true'
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                vox {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

configurations {
    include
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url = 'https://repo.spongepowered.org/repository/maven-public/'
    }
    maven {
        url = 'https://jitpack.io/'
    }
    maven {
        url = 'https://libraries.minecraft.net'
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2860'
    include 'org.apache.commons:commons-lang3:3.12.0'
    include 'com.mojang:brigadier:1.0.18'

    include('org.spongepowered:mixin:0.7.11-SNAPSHOT') {
        exclude module: 'gson'
        exclude module: 'guava'
        exclude module: 'commons-io'
        exclude module: 'launchwrapper'
        exclude module: 'log4j-core'
    }
    annotationProcessor('org.spongepowered:mixin:0.8.5:processor') {
        exclude module: 'gson'
    }

    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'
    implementation configurations.include
}

processResources {
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
    rename '(.+_at.cfg)', 'META-INF/$1'
}

mixin {
    add sourceSets.main, 'mixins.vox.refmap.json'
    config 'mixins.vox.json'
    debug.export = true
}

//reobf {
//    shadowJar {
//        duplicatesStrategy DuplicatesStrategy.EXCLUDE
//    }
//}

jar {
    manifest.attributes(
            'Manifest-Version': 1.0,
            'MixinConfigs': 'mixins.vox.json',
            'FMLAT': 'vox_at.cfg',
            'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
            'TweakOrder': 0,
            'FMLCorePluginContainsFMLMod': 'true',
            'FMLCorePlugin': 'dev.rabies.vox.MixinLoader',
            'ForceLoadAsMod': 'true'
    )

    from {
        configurations.include.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

jar.finalizedBy('reobfJar')
tasks.shadowJar.finalizedBy('reobfShadowJar')
